plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
}

def serversFile = file('servers.properties')
Properties serversProps = new Properties()
serversProps.load(new FileInputStream(serversFile))

def googleFile = file('google.properties')
Properties googleProps = new Properties()
googleProps.load(new FileInputStream(googleFile))

def facebookFile = file('facebook.properties')
Properties facebookProps = new Properties()
facebookProps.load(new FileInputStream(facebookFile))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            testCoverageEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            testCoverageEnabled project.hasProperty('coverage')
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    flavorDimensions 'server'
    productFlavors {
        development {
            dimension 'server'
            buildConfigField 'String', 'SERVER_V1', serversProps['SERVER_V1_DEV']
            resValue "string", "GOOGLE_WEB_CLIENT_ID", String.format("%s", googleProps['GOOGLE_WEB_CLIENT_ID_DEV'])
            resValue "string", "FB_APP_ID", String.format("%s", facebookProps['FB_APP_ID_DEV'])
            resValue "string", "FB_LOGIN_PROTOCOL_SCHEME", String.format("fb%s", facebookProps['FB_APP_ID_DEV'])
        }
        staging {
            dimension 'server'
            buildConfigField 'String', 'SERVER_V1', serversProps['SERVER_V1_STAGE']
            resValue "string", "GOOGLE_WEB_CLIENT_ID", String.format("%s", googleProps['GOOGLE_WEB_CLIENT_ID_STAGE'])
            resValue "string", "FB_APP_ID", String.format("%s", facebookProps['FB_APP_ID_STAGE'])
            resValue "string", "FB_LOGIN_PROTOCOL_SCHEME", String.format("fb%s", facebookProps['FB_APP_ID_STAGE'])
        }
        beta {
            dimension 'server'
            buildConfigField 'String', 'SERVER_V1', serversProps['SERVER_V1_BETA']
            resValue "string", "GOOGLE_WEB_CLIENT_ID", String.format("%s", googleProps['GOOGLE_WEB_CLIENT_ID_BETA'])
            resValue "string", "FB_APP_ID", String.format("%s", facebookProps['FB_APP_ID_BETA'])
            resValue "string", "FB_LOGIN_PROTOCOL_SCHEME", String.format("fb%s", facebookProps['FB_APP_ID_BETA'])
        }
        production {
            dimension 'server'
            buildConfigField 'String', 'SERVER_V1', serversProps['SERVER_V1_STORE']
            resValue "string", "GOOGLE_WEB_CLIENT_ID", String.format("%s", googleProps['GOOGLE_WEB_CLIENT_ID_STORE'])
            resValue "string", "FB_APP_ID", String.format("%s", facebookProps['FB_APP_ID_STORE'])
            resValue "string", "FB_LOGIN_PROTOCOL_SCHEME", String.format("fb%s", facebookProps['FB_APP_ID_STORE'])
        }
    }
    buildFeatures {
        dataBinding = true
    }
}

dependencies {
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation project(path: ':stockbitcorelib')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.2.1'

    def room_version = "2.3.0"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    def lifecycle_version = "2.3.1"
    def arch_version = "2.1.0"

    //Lifecycle
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // Lifecycles only (without ViewModel or LiveData)
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    // alternately - if using Java8, use the following instead of lifecycle-compiler
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // optional - helpers for implementing LifecycleOwner in a Service
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"

    // optional - ProcessLifecycleOwner provides a lifecycle for the whole application process
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"

    // optional - ReactiveStreams support for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"

    // optional - Test helpers for LiveData
    testImplementation "androidx.arch.core:core-testing:$arch_version"

    implementation "androidx.activity:activity-ktx:1.2.3"
    implementation "androidx.fragment:fragment-ktx:1.3.4"

    // Logger & Debugger
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.facebook.stetho:stetho:1.6.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.6.0'

    // Network
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation "com.github.haroldadmin:NetworkResponseAdapter:3.0.1"

    implementation "androidx.multidex:multidex:2.0.0"

    def koin_version = "2.0.1"
    implementation "io.insert-koin:koin-androidx-viewmodel:$koin_version"

    def navigation_version = "2.3.1"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Login
    implementation 'com.google.android.gms:play-services-auth:19.2.0'
    implementation 'com.facebook.android:facebook-android-sdk:[4,5)'
    implementation "androidx.biometric:biometric:1.0.1"

    def mockito_version = "3.1.0"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    testImplementation 'junit:junit:4.12'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2"
}